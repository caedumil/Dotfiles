#
# Caedus theme
# fork of Minimal theme (https://github.com/S1cK94/minimal)
#
#
# Colors
#    0 - black
#    1 - red
#    2 - green
#    3 - yellow
#    4 - blue
#    5 - magenta
#    6 - cyan
#    7 - white
#    8 - bright black
#    9 - bright red
#   10 - bright green
#   11 - bright yellow
#   12 - bright blue
#   13 - bright magenta
#   14 - bright cyan
#   15 - bright white
#

prompt_caedus_remote() {
    [[ -n ${SSH_TTY} ]] && print " %F{9}%n%f ${stop} %F{1}%m%f ${stop}"
}

prompt_caedus_path() {
    local -a wd
    local path_c="%F{4}"
    local sep="%F{8}/${path_c}"

    for d in "${(s:/:)${PWD/#$HOME/~}}"; do
        if [[ ${#d} -gt 10 ]]; then
            d="${d:0:3}…"
        fi

        wd+="${d}"
    done

    print " ${path_c}$(sed "s_/_${sep}_g" <<< ${(j:/:)wd})%f ${stop}"
}

prompt_caedus_git() {
    if [[ -n ${vcs_info_msg_0_} ]]; then
        print " $(sed -r "s_(:|\[|\]|\|)+_%F{8}\1%f_g" <<< ${vcs_info_msg_0_}) ${stop}"
    fi
}

+vi-git-branch() {
    local ahead behind
    local -a gitstatus

    ahead=$(git rev-list --count ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null)
    (( $ahead )) && gitstatus+=( "⇡${ahead}" )

    behind=$(git rev-list --count HEAD..${hook_com[branch]}@{upstream} 2>/dev/null)
    (( $behind )) && gitstatus+=( "⇣${behind}" )

    if (( ${#gitstatus[@]} )); then
        hook_com[branch]+="%F{2}(%f%F{6}${(j:/:)gitstatus}%f%F{2})%f"
    fi
}

+vi-git-stash() {
    local stashes

    if [[ -s ${hook_com[base]}/.git/refs/stash ]]; then
        stashes=$(git stash list 2>/dev/null | wc -l)
        hook_com[misc]+="${stashes}⟅"
    fi
}

+vi-git-dirty() {
    local -i untracked

    untracked=$(git status --porcelain -u | grep '^??' | wc -l)
    (( $untracked )) && hook_com[unstaged]="✗${hook_com[unstaged]}"
}

prompt_caedus_precmd() {
    vcs_info

    PROMPT='\
${stop}$(prompt_caedus_remote)$(prompt_caedus_path)$(prompt_caedus_git)%f
 %(1j.${stop/$color/6}.${stop}) %(!.%F{1}.%F{$color})λ%f '
    RPROMPT='%(?.${stop}.${stop/$color/1})'
    SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_caedus_setup() {
    color="3"
    stop="%F{$color}◆%f"

    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' get-revision true
    zstyle ':vcs_info:*' check-for-changes true                 # enable both '%u' and '%c'
    zstyle ':vcs_info:*' check-for-staged-changes false         # enable only '%c'
    zstyle ':vcs_info:*' unstagedstr "⚡"
    zstyle ':vcs_info:*' stagedstr "→"
    zstyle ':vcs_info:git:*' formats "%F{2}%.7i%f:%F{2}%b%f [%F{1}%m%u%f%F{2}%c%f]"
    zstyle ':vcs_info:git:*' actionformats "%F{5}%a%f|%F{2}%b%f [%F{1}%m%u%f%F{2}c%f]"
    zstyle ':vcs_info:git*+set-message:*' hooks git-branch git-stash git-dirty

    add-zsh-hook precmd prompt_caedus_precmd
    prompt_opts=(cr subst percent)
}

prompt_caedus_setup "${@}"

# vim:filetype=zsh:textwidth=100
